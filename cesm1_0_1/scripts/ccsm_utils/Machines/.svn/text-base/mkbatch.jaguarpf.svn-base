#! /bin/csh -f

set mach = jaguarpf

#################################################################################
if ($PHASE == set_batch) then
#################################################################################

source ./Tools/ccsm_getenv || exit -1

set mppsize = `${UTILROOT}/Tools/taskmaker.pl -sumonly`
if ( $mppsize % ${MAX_TASKS_PER_NODE} > 0) then
  @ mppnodes = $mppsize / ${MAX_TASKS_PER_NODE}
  @ mppnodes = $mppnodes + 1
  @ mppsize = $mppnodes * ${MAX_TASKS_PER_NODE}
endif

#--- Job name is first fifteen characters of case name ---
set jobname = `echo ${CASE} | cut -c1-15`
set account_name = `showproj | tail -1`
if (-e ~/.ccsm_proj) then
   set account_name = `head -1 ~/.ccsm_proj`
endif

set walltime = "00:45:00"
if ($CCSM_ESTCOST > 1) set walltime = "01:50:00"
if ($CCSM_ESTCOST > 3) set walltime = "05:00:00"

cat >! $CASEROOT/${CASE}.${mach}.run << EOF1
#!/bin/csh -f
#===============================================================================
#  This is a CCSM batch job script for $mach
#===============================================================================

#PBS -A ${account_name}
#PBS -N ${jobname}
#PBS -q batch
##PBS -l feature=xt5
#PBS -l size=${mppsize}
#PBS -l walltime=${walltime}
#PBS -j oe
#PBS -S /bin/csh -V

EOF1

#################################################################################
else if ($PHASE == set_exe) then
#################################################################################

set maxthrds = `${UTILROOT}/Tools/taskmaker.pl -maxthrds`
set myaprun = `${UTILROOT}/Tools/taskmaker.pl -aprun`

cat >> ${CASEROOT}/${CASE}.${MACH}.run << EOF1
# -------------------------------------------------------------------------
# Run the model
# -------------------------------------------------------------------------

sleep 25
cd \$RUNDIR
echo "\`date\` -- CSM EXECUTION BEGINS HERE" 

setenv OMP_NUM_THREADS ${maxthrds}
aprun ${myaprun} >&! ccsm.log.\$LID
wait

echo "\`date\` -- CSM EXECUTION HAS FINISHED" 

EOF1


#################################################################################
else if ($PHASE == set_larch) then
#################################################################################


set account_name = `showproj | tail -1`
if (-e ~/.ccsm_proj) then
   set account_name = `head -1 ~/.ccsm_proj`
endif

cat >! $CASEROOT/${CASE}.${mach}.l_archive << EOF1
#! /bin/csh -f
#===============================================================================
#  This is a CCSM coupled model batch long term archiving script
#===============================================================================

#PBS -A ${account_name}
#PBS -N ${CASE}.l_archive
#PBS -q batch
#PBS -l size=0
#PBS -l walltime=01:30:00
#PBS -j oe
#PBS -S /bin/csh -V

#-----------------------------------------------------------------------
# Determine necessary environment variables
#-----------------------------------------------------------------------

cd $CASEROOT 

source ./Tools/ccsm_getenv || exit -1

#----------------------------------------------
# run the long term archiver
#----------------------------------------------

setenv LID "\`date +%y%m%d-%H%M%S\`"
\$CASETOOLS/ccsm_l_archive.csh  >&! ccsm_la.log.\$LID

EOF1


#################################################################################
else
#################################################################################

    echo "mkscripts.$mach"
    echo "  PHASE setting of $PHASE is not an accepted value"
    echo "  accepted values are set_batch, set_exe and set_larch"
    exit 1

#################################################################################
endif
#################################################################################
